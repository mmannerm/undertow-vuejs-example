plugins {
  id "com.github.node-gradle.node" version "2.2.3"
  id "com.github.ben-manes.versions" version "0.28.0"
  id "com.gorylenko.gradle-git-properties" version "2.2.2"
  id "org.ajoberstar.reckon" version "0.12.0"
  id "jacoco"
  id "org.beryx.runtime" version "1.8.0"
  id "maven"
}

ext {
  restEasyVersion = '4.4.2.Final'
  undertowVersion = '2.0.29.Final'
  junit5Version = '5.6.0'
  jacksonVersion = '2.10.2'
  log4jVersion = '2.13.0'
}

group 'com.gridmancer'
mainClassName = 'com.gridmancer.example.undertow.vuejs.Server'

sourceCompatibility = 11
targetCompatibility = 11

apply from: "common.gradle"
apply from: "jacoco.gradle"

repositories {
    jcenter()
}

configurations {
  // CVE-2014-8122
  implementation {
    exclude group: 'javax.enterprise', module: 'cdi-api'
  }
  api {
    exclude group: 'javax.enterprise', module: 'cdi-api'
  }
}

dependencies {
  testImplementation platform("org.junit:junit-bom:${junit5Version}")

  implementation platform("org.jboss.resteasy:resteasy-bom:${restEasyVersion}")
  implementation platform("io.undertow:undertow-parent:${undertowVersion}")
  implementation platform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
  implementation platform("org.apache.logging.log4j:log4j-bom:${log4jVersion}")

  implementation "org.apache.logging.log4j:log4j-slf4j-impl"
  implementation "com.lmax:disruptor:3.4.2"

  implementation "io.undertow:undertow-core"
  implementation "io.undertow:undertow-servlet";
  implementation "org.jboss.resteasy:resteasy-core-spi"
  implementation "org.jboss.resteasy:resteasy-jackson2-provider"
  // Override guava 16.0 coming from resteasy-jackson2-provider [CVE 2018-10237]
  implementation "com.google.guava:guava:28.2-jre"

  implementation "org.apache.httpcomponents:httpclient:4.5.11"

  testImplementation "org.jmockit:jmockit:1.49"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

reckon {
  scopeFromProp()
  snapshotFromProp()
}

reckonTagCreate.dependsOn check

test {
  useJUnitPlatform()
  jvmArgs "-javaagent:${classpath.find { it.name.contains('jmockit') }.absolutePath}"

  testLogging {
    events 'passed', 'skipped', 'failed'
  }

  reports {
    html.enabled = true
  }
}

task webpack(type: NpxTask) {
  dependsOn npmInstall
  command = 'webpack'
  args = [ '--config', 'node_modules/@vue/cli-service/webpack.config.js' ]

  inputs.files('package.json', 'package-lock.json', 'vue.config.js', 'babel.config.js').withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir('src/main/webapp').withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir('public').withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir(fileTree("node_modules").exclude(".cache"))
  outputs.dir("build/resources/main/static")
  outputs.cacheIf { true }
}

processResources.dependsOn('webpack')
assemble.dependsOn 'runtimeZip'
install.dependsOn 'runtimeZip'
clean.delete << file('node_modules')

runtime {
    imageZip = file("${buildDir}/${name}-${version}-image.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = [
        'java.naming',
        'java.management',
        'java.logging',
        'java.desktop', //log4j uses java.beans.PropertyChangeSupport which is part of this module
        'java.instrument',
        'jdk.management',
        'jdk.security.jgss',
        'jdk.crypto.ec',
        'jdk.unsupported'
    ]
}