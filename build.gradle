plugins {
  id "com.github.node-gradle.node" version "2.2.3"
  id "com.github.ben-manes.versions" version "0.28.0"
  id "com.gorylenko.gradle-git-properties" version "2.2.2"
  id "org.ajoberstar.reckon" version "0.12.0"
  id "com.palantir.docker" version "0.25.0"
  id "com.palantir.docker-compose" version "0.25.0"
  id "com.patdouble.cucumber-jvm" version "0.19"
  id "eu.leontebbens.gradle.chromedriver-updater" version "1.6.2"
  id "de.undercouch.download" version "4.0.4"
  id "jacoco"
  id "org.beryx.runtime" version "1.8.0"
  id "maven"
}

ext {
  restEasyVersion = '4.5.2.Final'
  undertowVersion = '2.0.30.Final'
  junit5Version = '5.6.0'
  jacksonVersion = '2.10.3'
  log4jVersion = '2.13.1'
  bouncyCastleVersion = '1.64'
  smallryeConfigVersion = '1.7.0'
  assertJVersion = '3.15.0'
  httpComponentsVersion = '4.5.12'
  disruptorVersion = '3.4.2'
  jmockitVersion = '1.49'
  cucumberVersion = '5.5.0'
  seleniumVersion = '3.141.59'
}

group 'com.gridmancer'
mainClassName = 'com.gridmancer.example.undertow.vuejs.Server'

sourceCompatibility = 11
targetCompatibility = 11

reckon {
  scopeFromProp()
  snapshotFromProp()
}

reckonTagCreate.dependsOn check

apply from: "gradle-common/common.gradle"
apply from: "gradle-common/jacoco.gradle"
apply from: "gradle-common/container.gradle"

repositories {
    jcenter()
    mavenCentral()
}

configurations {
  // CVE-2014-8122
  implementation {
    exclude group: 'javax.enterprise', module: 'cdi-api'
  }
  api {
    exclude group: 'javax.enterprise', module: 'cdi-api'
  }
}

addCucumberSuite 'acceptance'

dependencies {
  testImplementation platform("org.junit:junit-bom:${junit5Version}")
  acceptanceImplementation platform("org.junit:junit-bom:${junit5Version}")
  acceptanceImplementation platform("io.cucumber:cucumber-jvm:${cucumberVersion}")

  implementation platform("org.jboss.resteasy:resteasy-bom:${restEasyVersion}")
  implementation platform("io.undertow:undertow-parent:${undertowVersion}")
  implementation platform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
  implementation platform("org.apache.logging.log4j:log4j-bom:${log4jVersion}")

  implementation "org.apache.logging.log4j:log4j-slf4j-impl"
  implementation "com.lmax:disruptor:${disruptorVersion}"

  implementation "io.undertow:undertow-core"
  implementation "io.undertow:undertow-servlet"
  implementation "org.jboss.resteasy:resteasy-core:${restEasyVersion}"
  implementation "org.jboss.resteasy:resteasy-jackson2-provider"
  // Override guava 16.0 coming from resteasy-jackson2-provider [CVE 2018-10237]
  implementation "com.google.guava:guava:28.2-jre"
  implementation "io.smallrye.config:smallrye-config:${smallryeConfigVersion}"
  implementation "io.smallrye.config:smallrye-config-source-yaml:${smallryeConfigVersion}"

  implementation "org.apache.httpcomponents:httpclient:${httpComponentsVersion}"

  implementation "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
  implementation "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"

  testImplementation "org.jmockit:jmockit:${jmockitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.assertj:assertj-core:${assertJVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

  acceptanceImplementation "io.cucumber:cucumber-java8"
  acceptanceImplementation "io.cucumber:cucumber-junit"
  acceptanceImplementation "org.junit.jupiter:junit-jupiter-api"
  acceptanceImplementation "org.assertj:assertj-core:${assertJVersion}"
  acceptanceImplementation "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
  acceptanceImplementation "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
  acceptanceRuntimeOnly "org.junit.vintage:junit-vintage-engine"
}

acceptance {
  dependsOn updateChromedriver

  isStrict = true
  junitReport = true
  systemProperties = [
    "webdriver.chrome.driver": "${updateChromedriver.driverLocation}",
    "testHost": "localhost",
    "testPort": "4443"
  ]
}

test {
  useJUnitPlatform()
  jvmArgs "-javaagent:${classpath.find { it.name.contains('jmockit') }.absolutePath}"

  testLogging {
    events 'passed', 'skipped', 'failed'
  }

  reports {
    html.enabled = true
  }
}

task webpack(type: NpxTask) {
  dependsOn npmInstall
  command = 'webpack'
  args = [ '--config', 'node_modules/@vue/cli-service/webpack.config.js' ]

  inputs.files('package.json', 'package-lock.json', 'vue.config.js', 'babel.config.js').withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir('src/main/webapp').withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir('public').withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir(fileTree("node_modules").exclude(".cache"))
  outputs.dir("build/resources/main/static")
  outputs.cacheIf { true }
}

processResources.dependsOn('webpack')
assemble.dependsOn 'runtimeZip'
install.dependsOn 'runtimeZip'
clean.delete << file('node_modules')

runtime {
    imageZip = file("${buildDir}/${name}-${version}-image.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = [
        'java.naming',
        'java.management',
        'java.logging',
        'java.desktop', //log4j uses java.beans.PropertyChangeSupport which is part of this module
        'java.instrument',
        'jdk.management',
        'jdk.security.jgss',
        'jdk.crypto.ec',
        'jdk.unsupported'
    ]
}

acceptance.dependsOn 'dockerComposeUp'