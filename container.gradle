apply plugin: "de.undercouch.download"
apply plugin: "com.palantir.docker"
apply plugin: "org.beryx.runtime"

def cacheDir = "${gradle.gradleUserHomeDir}/caches/runtime-jdk"
def jdkDownloadURI = 'https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.6%2B10/OpenJDK11U-jdk_x64_linux_hotspot_11.0.6_10.tar.gz'
def jdkBaseName = Paths.get(new URI(jdkDownloadURI).getPath()).getFileName().toString().split("\\.")[0]
def jdkDir = "${cacheDir}/${jdkBaseName}" //jdk_x64_linux_hotspot_11.0.6_10"

import java.nio.file.Paths;

task downloadLinuxJRE(type: Download) {
  src jdkDownloadURI
  dest new File("${cacheDir}/downloads/", Paths.get(src.getPath()).getFileName().toString())
  overwrite false
  doLast{
    delete "${jdkDir}"
    mkdir "${jdkDir}"
  }
}

task extractLinuxJRE(dependsOn: downloadLinuxJRE, type: Copy) {
  from(tarTree(resources.gzip(downloadLinuxJRE.dest))) {
    eachFile { fcd ->
      fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(1))
    }
    includeEmptyDirs = false
  }
  into "${jdkDir}"
}

docker {
    name "${project.name}:${project.version}"
    files tasks.runtime.imageDir, ".dockerignore"
}

runtime {
    targetPlatform('linux-x64', "${jdkDir}")
}

tasks.runtime.dependsOn 'extractLinuxJRE'